plugins {
    id 'jacoco'
    id 'groovy'
    id 'maven'
    id 'nebula.provided-base' version '2.2.2'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'nu.studer.plugindev' version '1.0.4'
    id 'com.jfrog.bintray' version '1.3.1'
    id 'com.github.kt3k.coveralls' version '2.4.0'
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    provided 'com.android.tools.build:gradle:1.3.+'

    testCompile 'com.netflix.nebula:nebula-test:3.0.0'
}

test {
    testLogging {
        displayGranularity = 2
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

final PLUGIN_VERSION = '0.2.0'
final PACKAGE_NAME = 'hihex.fleamarket'
final PUBLIC_URL = 'https://github.com/hihex/fleamarket'
final TAGS = ['android', 'apk', 'multiple', 'channel', 'patch', 'modify']
final DESCRIPTION = 'Create APKs for multiple channels quickly, and allows channel-specific resource modification'

version = PLUGIN_VERSION
group = PACKAGE_NAME

plugindev {
    pluginImplementationClass 'hihex.fleamarket.FleaMarketPlugin'
    pluginDescription DESCRIPTION
    pluginLicenses 'GPL-3.0'
    pluginTags (*TAGS)
    authorId 'kennytm'
    authorName 'kennytm'
    authorEmail 'kennytm@hihex.com'
    projectUrl PUBLIC_URL
    projectInceptionYear '2015'
    done()
}

bintray {
    //dryRun = true
    publish = false

    user = 'kennytm'
    key = ''//BINTRAY_API_KEY
    pkg {
        repo = 'maven'
        name = PACKAGE_NAME
        userOrg = 'hihex'
    }
}

pluginBundle {
    website = PUBLIC_URL
    vcsUrl = PUBLIC_URL
    description = DESCRIPTION
    tags = TAGS

    plugins {
        fleamarketPlugin {
            id = PACKAGE_NAME
            displayName = 'HiHex FleaMarket'
        }
    }
}
